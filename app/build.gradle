plugins {
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

android {
    compileSdk 33
    buildToolsVersion = "31.0.0"

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'mozilla/public-suffix-list.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/*.kotlin_module']
        }
    }

    defaultConfig {
        applicationId "com.astarivi.kaizoyu"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 10
        versionName "0.9"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_k_name", "Kaizoyu! Debug"
            resValue "string", "provider_authority", "com.astarivi.kaizoyu.debug.fileprovider"
        }

        beta {
            applicationIdSuffix '.beta'
            versionNameSuffix '01-BETA'
            resValue "string", "app_k_name", "Kaizoyu! Beta"
            resValue "string", "provider_authority", "com.astarivi.kaizoyu.beta.fileprovider"
        }

        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_k_name", "Kaizoyu!"
            resValue "string", "provider_authority", "com.astarivi.kaizoyu.fileprovider"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.astarivi.kaizoyu'

    splits {
        abi {
            enable true
            reset()

            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }
}

dependencies {
    // Core library
    implementation project(':service')

    // External libraries (third party)
    implementation 'com.github.WrichikBasu:AudioFocusController:v2.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'
    implementation 'com.github.astarivi:Zparc:d0b7473283'
    implementation 'org.videolan.android:libvlc-all:4.0.0-eap11'
    implementation 'com.airbnb.android:lottie:6.0.0'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.github.bumptech.glide:recyclerview-integration:4.15.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.15.1'
    implementation 'org.jetbrains:annotations:24.0.1'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:12.0.0'
    implementation 'com.rometools:rome:2.1.0'

    // Lombok
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    //noinspection AnnotationProcessorOnCompilePath
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    // Analytics
    implementation 'com.flurry.android:analytics:14.2.0'

    // Android X
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.6.1'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.room:room-runtime:2.5.2'
    annotationProcessor 'androidx.room:room-compiler:2.5.2'

    // Logger
    implementation 'org.tinylog:tinylog-api:2.6.1'
    implementation 'org.tinylog:tinylog-impl:2.6.1'
    runtimeOnly 'org.tinylog:slf4j-tinylog:2.6.1'

    // Jackson
    // Don't update to 2.14 as Android minSdk support changes.

    //noinspection GradleDependency
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.5'
    //noinspection GradleDependency
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.5'
    //noinspection GradleDependency
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.5'

    // HTTP
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    // Google others
    implementation 'com.google.android.material:material:1.9.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    // Testing? What's that
}
